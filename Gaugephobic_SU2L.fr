(* ********************************************************* *)
(* *****                                               ***** *)
(* ***** FeynRules model file: SU(2) Dark Pion and Rho ***** *)
(* *****  Author: B. Ostdiek, T. Tong                  ***** *)
(* *****  Version:  1.0                                ***** *)
(* ********************************************************* *)

(* ************************** *)
(* *****  Information   ***** *)
(* ************************** *)
M$ModelName = "Gaugephobic_SU2L";
M$Information = { Authors->{"Bryan Ostdiek, Tom Tong"},
                  Emails->{"bostdiek@uoregon.edu"},
                  Institutions->{"University of Oregon"},
                  Version->"1.0"};

(* ************************** *)
(* *****  Change Log    ***** *)
(* ************************** *)
(* v1.0 Paper parameterizations *)
(* v0.8 Fixed the LRhoDPGauge terms with an extra factor of 2 *)
(* v0.7 Fixed the LDPHiggsGauge terms*)
(* v0.2 Change parameters to match those in the U(1) case *)
(* v0.1 Added ms-bar masses for the quarks which affects the pion fermionic decay *)

FeynmanGauge = True;


(* ************************** *)
(* *** Interaction orders *** *)
(* ************************** *)
M$InteractionOrderHierarchy = { {QCD, 1}, {QED, 2}, {NP,1} };

(* ************************** *)
(* *****   New Fields   ***** *)
(* ************************** *)
M$ClassesDescription = {
(* Vectors (RHOS) *)
  V[97] == {
    ClassName       -> Rho0,
    SelfConjugate   -> True,
    Mass            -> {Mro, Internal},
    Width           -> {Wro, 1},
    ParticleName    -> "rho0",
    PropagatorLabel -> "rho0",
    PropagatorType  -> W,
    PropagatorArrow -> None,
    FullName        -> "Dark Rho"
  },
  V[98] == {
    ClassName       -> Rho,
    SelfConjugate   -> False,
    Mass            -> {Mro, Internal},
    Width           -> {Wrop, 1},
    ParticleName    -> "rho+",
    AntiParticleName -> "rho-",
    QuantumNumbers   -> {Q -> 1},
    PropagatorLabel -> "rho+",
    PropagatorType  -> W,
    PropagatorArrow -> None,
    FullName        -> "Dark Rho Charged"
  },
  V[99] == {
      ClassName     -> Rhoi,
      Unphysical    -> True,
      SelfConjugate -> True,
      Indices       -> {Index[SU2W]},
      FlavorIndex   -> SU2W,
      Definitions   -> { Rhoi[mu_,1] -> (Rhobar[mu]+Rho[mu])/Sqrt[2],
                         Rhoi[mu_,2] -> (Rhobar[mu]-Rho[mu])/(I*Sqrt[2]),
                         Rhoi[mu_,3] -> Rho0[mu]}
    },
(* Scalars (PIONS) *)
  S[12] == {
    ClassName -> DP,
    SelfConjugate -> False,
    Mass -> {MDPc, Internal},
    Width -> {WDP, 5},
    QuantumNumbers -> {Q->1},
    ParticleName -> "DP+",
    AntiParticleName -> "DP-"
    },
S[13] == {
    ClassName -> DP0,
    SelfConjugate -> True,
    Mass -> {MDP, Internal},
    Width -> {WDP0, 5},
    ParticleName -> "DP0"
    },
S[14] == {
    ClassName -> DPi,
    Unphysical ->True,
    Indices -> {Index[SU2W]},
    SelfConjugate -> True,
    FlavorIndex   -> SU2W,
    Definitions   -> { DPi[1] -> (DPbar+DP)/Sqrt[2],
                       DPi[2] -> (DPbar-DP)/(I*Sqrt[2]),
                       DPi[3] -> DP0
                     }
    }
};


(* ************************** *)
(* *****   Parameters   ***** *)
(* ************************** *)
M$Parameters = {
(* External parameters *)
  NDark == {
    ParamterType -> External,
    Value        -> 4,
    Description  -> "Number of dark colors",
    InteractionOrder -> {NP,0}
  },
  PionMass == {
    ParameterType -> External,
    Value         -> 150.0,
    TeX           -> Subscript[M,\[Pi]],
    Description   -> "Dark Pion mass"
  },
  FermionEta == {
    ParameterType -> External,
    Value         -> 0.45,
    TeX           -> \[Eta],
    Description   -> "Ratio of the dark pion mass to the dark rho mass"
  },
  VPi == {
    ParameterType -> External,
    Value         -> 10000.0,
    Description   -> "Controls coupling of dark pions to fermions [Equation 12]",
    InteractionOrder -> {QED,-1}
  },
  Xi == {
    ParamterType -> External,
    Value        -> 0.1,
    Description  -> "Controls coupling of dark pions to gauge-higss [Equation 12]",
    InteractionOrder -> {NP,0}
  },

(* Internal parameters *)

  CL == {
    ParameterType-> Internal,
    Value -> 4*Pi,
    Description -> "Constant for mesons",
    InteractionOrder -> {NP, 1}
  },
  Mro == {
    ParameterType -> Internal,
    Value         -> PionMass / FermionEta,
    TeX           -> Subscript[M,\[Rho]],
    Description   -> "Dark Rho mass"
  },
  MDP == {
    ParameterType -> Internal,
    Value         -> PionMass,
    Description   -> "Dark pion mass"
  },
  MDPc == {
    ParameterType -> Internal,
    Value         -> PionMass,
    Description   -> "Dark pion mass charged"
  },
  msbarUpQuark == {
    ParameterType    -> Internal,
    Indices          -> {Index[Generation]},
    ComplexParameter -> False,
    Value            -> {msbarUpQuark[1] -> 0,
                         msbarUpQuark[2] -> 0.616,  (* charm quark msbar mass at mH *)
                         msbarUpQuark[3] -> 169.2   (* top quark msbar mass at mH *)
                         },
    Description      -> "Up type quark msbar masses"
  },
  msbarDownQuark == {
    ParameterType    -> Internal,
    Indices          -> {Index[Generation]},
    ComplexParameter -> False,
    Value            -> {msbarDownQuark[1] -> 0,
                         msbarDownQuark[2] -> 0,    (* strange quark msbar mass at mH *)
                         msbarDownQuark[3] -> 2.76  (* bottom quark msbar mass at mH *)
                         },
    Description      -> "Down type quark msbar masses"
  },


  (*Non-Rho Couplings*)

  cPiHiggsGauge == {
    ParameterType -> Internal,
    Definitions -> {cPiHiggsGauge -> - Xi * MW / VPi},
    Description  -> "Coupling between the dark pion, Higgs, and gauge bosons",
    InteractionOrder -> {NP, 1}
  },
  cPiFermion == {
    ParameterType -> Internal,
    Definitions -> {cPiFermion -> Sqrt[2] / VPi },
    InteractionOrder -> {NP, 1},
    Description  -> "Coupling between the dark pion and SM Fermions"
  },

  (*Rho Couplings*)

  cRhoPiPi == {
    ParameterType -> Internal,
    Definitions -> {cRhoPiPi -> (CL)/Sqrt[NDark]},
    InteractionOrder -> {NP, 1},
    Description -> "Strong coupling between the dark mesons"
  },
  cKineticMixing == {
    ParameterType -> Internal,
    Definitions -> {cKineticMixing -> Sqrt[NDark]/(4*Pi) * gw},
    (*InteractionOrder -> {NP, 1},*)
    Description-> "Mixing between the dark rho and the SU(2)_L bosons"
  }
};


(* New Lagrangian interaction terms *)

(*Dark Pions without Rhos*)
LDPKin = Block[{mu, i},ExpandIndices[1/2 * DC[DPi[i], mu] DC[DPi[i],mu] - PionMass^2/2  DPi[i] DPi[i],FlavorExpand->{SU2D,SU2W}]];

LDPQuark := Block[{s, f, r, tmp},
                  tmp = cPiFermion * ((DP*uqbar[s, f, a].(msbarDownQuark[f] ProjP[s,r] - msbarUpQuark[f] ProjM[s,r]).dq[r, f, a]) +
                                (DPbar dqbar[s, f, a].(msbarDownQuark[f] ProjM[s,r] - msbarUpQuark[f] ProjP[s,r]).uq[r,f,a]) +
                                I/Sqrt[2] * DP0(msbarUpQuark[f] uqbar[s,f,a].(ProjP[s,r]-ProjM[s,r]).uq[r,f,a] -
                                                msbarDownQuark[f] dqbar[s,f,a].(ProjP[s,r]-ProjM[s,r]).dq[r,f,a]
                                                )
                               );
                  ExpandIndices[tmp,FlavorExpand->{SU2D,SU2W}]
                  ];


LDPLepton := Block[{s, f, r, tmp},
                   tmp = cPiFermion * ((DP * vlbar[s, f].(Ml[f] ProjP[s,r] ).l[r,f]) +
                                       (DPbar lbar[s, f].(Ml[f] ProjM[s,r] ).vl[r, f]) +
                                       I/Sqrt[2] * DP0(-Ml[f] lbar[s, f].(ProjP[s,r]-ProjM[s,r]).l[r, f]
                                          )
                                      );
                  ExpandIndices[tmp,FlavorExpand->{SU2D,SU2W}]
                  ];

LDPFermions := ExpandIndices[LDPQuark + LDPLepton,FlavorExpand->{SU2D,SU2W}];

LDPHiggsGauge := Block[{mu},
                       ExpandIndices[
                       cPiHiggsGauge * (H Wbar[mu] del[DP, mu] - del[H, mu] Wbar[mu] DP + H W[mu] del[DPbar, mu] - del[H, mu] W[mu] DPbar + 1/cw (H Z[mu] del[DP0, mu] - del[H, mu] Z[mu] DP0))
                      ]
                      ];

(* Dark Rho Terms *)
LDarkRhoKinetic := Block[{mu,nu, i}, ExpandIndices[- 1/4 (del[Rhoi[nu,i],mu]-del[Rhoi[mu,i],nu])(del[Rhoi[nu,i],mu]-del[Rhoi[mu,i],nu])
                                                + Mro^2/2 Rhoi[mu,i]Rhoi[mu,i],
                                                FlavorExpand->SU2W]
                        ];

LRhoDPDP := Block[{mu, a, b, c},ExpandIndices[cRhoPiPi * (Rhoi[mu,a] * DPi[b] del[DPi[c],mu] Eps[a,b,c])]];

LKineticMixing := Block[{mu, a, b, c},
ExpandIndices[gw cKineticMixing (LLbar.ProjM[mu].Ta[a].LL Rhoi[mu,a] +  QLbar.ProjM[mu].Ta[a].QL Rhoi[mu,a] )
        ,FlavorExpand->{SU2W,SU2D}]/.{CKM[a_,b_] Conjugate[CKM[a_,c_]]->IndexDelta[b,c], CKM[b_,a_] Conjugate[CKM[c_,a_]]->IndexDelta[b,c]}
];

(* Everything Combined *)
LDarkSector = ExpandIndices[LDPKin + LDPFermions + LDPHiggsGauge +
              LDarkRhoKinetic + LRhoDPDP + LKineticMixing];
